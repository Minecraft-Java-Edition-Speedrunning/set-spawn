plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
	id 'org.ajoberstar.grgit' version '4.1.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def getVersionMetadata() {
	// CI builds version numbers
	def build_id = System.getenv("RELEASE_TAG")
	if (build_id != null) {
		return build_id
	}

	// Development builds
	if (grgit == null) {
		return "dev"
	}

	// Named development builds
	def id = grgit.head().abbreviatedId
	if (!grgit.status().clean) {
		id += "-dirty"
	}

	return "rev.${id}"
}

archivesBaseName = "${project.mod_id}-${project.target_version}"
version = "${getVersionMetadata()}"

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
	inputs.property "mod_id", project.mod_id
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "mod_id": project.mod_id, "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE")
}